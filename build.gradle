plugins {
  id 'java'
  id 'org.springframework.boot' version '3.1.4'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'com.diffplug.eclipse.apt' version '3.42.2'
  id 'nu.studer.jooq' version '8.2.1'
}

group = 'io.github.krloxz.fws'
version = '0.0.1-SNAPSHOT'

java {
  sourceCompatibility = '17'
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

ext {
  set('immutablesVersion', '2.9.3')
  set('jooq.version', jooq.version.get())
  set('junit-jupiter.version', '5.10.0')
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  //implementation 'org.springframework.boot:spring-boot-starter-hateoas'
  implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
  implementation 'org.springframework.boot:spring-boot-starter-jooq'
  // implementation 'org.springframework:spring-jdbc'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  // Immutables
  implementation "org.immutables:value-annotations:${immutablesVersion}"
  annotationProcessor "org.immutables:value-processor:${immutablesVersion}"
  testAnnotationProcessor "org.immutables:value-processor:${immutablesVersion}"

  implementation 'org.flywaydb:flyway-core'
  runtimeOnly 'io.r2dbc:r2dbc-h2'
  jooqGenerator 'org.jooq:jooq-meta-extensions'
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'
  testImplementation 'net.javacrumbs.json-unit:json-unit-spring:3.2.2'
}

jooq {
  configurations {
    main {
      generationTool {
        logging = org.jooq.meta.jaxb.Logging.WARN
        jdbc = null
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
            schemata {
              schema {
                inputSchema = 'freelancer'
                outputSchema = 'freelancer'
              }
            }
            properties {
              property {
                key = 'scripts'
                value = "${projectDir}/src/*/resources/db/migration/*.sql"
              }
              property {
                key = 'sort'
                value = 'flyway'
              }
              property {
                key = 'unqualifiedSchema'
                value = 'none'
              }
              property {
                key = 'defaultNameCase'
                value = 'lower'
              }
            }
          }
          generate {
            relations = true
            records = true
            immutablePojos = true
            fluentSetters = true
          }
          target {
            packageName = 'io.github.krloxz.fws.infra.jooq'
            directory = 'build/generated/sources/jooq'
          }
        }
      }
    }
  }
}

eclipse {
  synchronizationTasks generateJooq
  classpath {
    downloadSources = true
  }
}

tasks.named('test') {
  useJUnitPlatform()
}
